apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '0.1.1'
group 'com.sergo_smyk.recycler-binding'

def props = new Properties()
file("local.properties").withInputStream { props.load(it) }


task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact sourcesJar
            artifact("$buildDir/outputs/aar/recycler-binding-release.aar")
            groupId this.group
            artifactId 'recycler-binding'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = props.getProperty('user') ?: System.getenv('BINTRAY_USER')
    key = props.getProperty('apiKey') ?: System.getenv('BINTRAY_API_KEY')

    publications = ['Production']
    configurations = ['archives']
    dryRun = false
    override = true
    publish = true
    pkg {
        repo = 'RecyclerBinding'
        name = 'com.sergo_smyk.recycler-binding'
        version {
            name = this.version
            released = new Date()
            vcsTag = this.version
        }
    }
}